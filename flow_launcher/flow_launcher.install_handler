#!/bin/tcsh -f
#
#
#    This script attempts to register the Flow Launcher as the protocol hander for
#    links that look like flow-launcher://blah.  
#
#    It should be sufficient for gnome apps like pidgin and kde apps
#    like konqueror.  Firefox seems to pay attention to the gnome
#    settings at least to the degree that it recognizes links of the
#    form flow-launcher://blah as hot-links, but it may still ask you to
#    select the application the first time you click on one.
#
#

set flowlauncher = "/usr/bin/ayon run /pipe/ayon/flow_launcher/flow_launcher.py"
set flowlauncher_staging = "/usr/bin/ayon run --use-staging /pipe/ayon/flow_launcher/flow_launcher.py"

#
#   Gnome: register flow-launcher protocol handler
#
echo "Installing flow-launcher protocol handler for Gnome."

set gconfTool = "`which gconftool-2`"

if ($status == 0) then
    gconftool-2 --set --type=string /desktop/gnome/url-handlers/flow-launcher/command /usr/bin/ayon' run /pipe/ayon/flow_launcher/flow_launcher.py "%s"'
    gconftool-2 --set --type=bool /desktop/gnome/url-handlers/flow-launcher/enabled true
    gconftool-2 --set --type=bool /desktop/gnome/url-handlers/flow-launcher/need-terminal false
else
    echo "WARNING: gconftool-2 not found: skipping gnome url-handler registration."
endif

#
#   KDE: register flow-launcher protocol handler
#   If you want to install this system wide copy the flow-launcher.protocol file to /usr/share/kde4/services/
#
echo "Installing flow-launcher protocol handler for KDE."

set kdeProtoDir = ~/.kde/share/services
if ( -e ~/.kde4/share/services ) then
    set kdeProtoDir = ~/.kde4/share/services
endif
if (${?KDEDIR}) then
    set kdeProtoDir = ${KDEDIR}/share/services
endif

if ( ! -e $kdeProtoDir) then
    mkdir -p $kdeProtoDir
endif

if ( -e $kdeProtoDir) then
    set kdeProtoFile = $kdeProtoDir/flow-launcher.protocol
    rm -f $kdeProtoFile
    cat > $kdeProtoFile << EOF
[Protocol]
exec=$flowlauncher "%u"
protocol=flow-launcher
input=none
output=none
helper=true
listing=false
reading=false
writing=false
makedir=false
deleting=false
EOF
    echo "Successfully created ${kdeProtoFile}"
else
    echo "WARNING: can't find or create KDE protocol directory: ${kdeProtoDir}:  skipping KDE url-handler registration."
endif

#
#   Gnome part 2 for xdg/Chrome: register flow-launcher protocol handler
#   If you want to install this system wide run the desktop-file-install as sudo/root
#   and remove the --dir= bit.
#
echo "Installing flow-launcher protocol handler for XDG"

set desktopInstall = "`which desktop-file-install`"

if ($status == 0) then
    set xdgDir = $HOME/.local/share/applications
    if ( ! -e $xdgDir ) then
        mkdir -p $xdgDir
    endif

    if ( -e $xdgDir) then
        set xdgFile = $xdgDir/flow-launcher.desktop
        rm -f $xdgFile
        cat > $xdgFile << EOF
[Desktop Entry]
Name=FlowLauncher
GenericName=FlowLauncher
Comment=Launch AYON launcher applications from Flow
Exec=$flowlauncher %U
NoDisplay=true
Terminal=false
Type=Application
MimeType=x-scheme-handler/flow-launcher;application/flow-launcher;
Keywords=FlowLauncher;
Actions=Staging;

[Desktop Action Staging]
Name=FlowLauncher (staging)
Exec=$flowlauncher_staging %u
EOF
        echo "Successfully created ${xdgFile}"
    else
        echo "WARNING: can't find or create XDG directory: ${xdgDir}:  skipping XDG url-handler registration."
    endif

    desktop-file-install $xdgFile --rebuild-mime-info-cache --dir=$xdgDir
else
    echo "WARNING: desktop-file-install not found: skipping xdg-handler registration."
endif

echo "Done."
